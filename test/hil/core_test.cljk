(ns hil.core-test
  (:require [clojure.test :refer [deftest is]]
            [hil.core :as hil]))

(deftest approval-request-is-minimal-and-valid
  (let [request (hil/approval-request {:id "ngc-terms"
                                       :title "Review NVIDIA terms"
                                       :summary "NVIDIA terms are ready for review."
                                       :action "Accept terms"
                                       :impact "Creates an external account."})]
    (is (= "NVIDIA terms are ready for review.\n\nAction: Accept terms\nImpact: Creates an external account."
           (hil/alert-text request)))
    (is (= :approved (hil/request! (hil/mock-prompt :approved) request)))))

(deftest invalid-decisions-are-rejected
  (is (thrown? #?(:clj Exception :cljs js/Error)
               (hil/request! (hil/mock-prompt :maybe)
                             {:id "x" :title "t" :summary "s" :action "a"}))))

(deftest alert-fields-are-kept-compact
  (is (thrown? #?(:clj Exception :cljs js/Error)
               (hil/approval-request {:id "x" :title "t"
                                      :summary (apply str (repeat 281 "x"))
                                      :action "a"}))))

(deftest native-dialog-input-is-non-secret
  (is (= {:decision :approved :input "kotoba-lang"}
         (hil/request-with-input!
          (hil/mock-prompt (fn [_] {:decision :approved :input "kotoba-lang"}))
          {:id "org" :title "Organization" :summary "Enter organization name."
           :action "Continue" :input-label "Organization name"})))
  (is (thrown? #?(:clj Exception :cljs js/Error)
               (hil/approval-request {:id "secret" :title "x" :summary "x"
                                      :action "x" :input-label "API key"}))))
